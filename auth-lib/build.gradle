/*
 * Copyright (c) 2015-2016 Spotify AB
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'com.android.library'

project.group = 'com.spotify.android'
project.archivesBaseName = 'auth'
project.version = '1.2.6'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 2
        versionName project.version
        buildConfigField 'String', 'LIB_VERSION_NAME', "\"$project.version\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    lintOptions {
        lintConfig file("${project.rootDir}/config/lint.xml")
        quiet false
        warningsAsErrors false
        textReport true
        textOutput 'stdout'
        xmlReport false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.21.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
}

/*
    Static analysis section
    run: ./gradlew auth-lib:checkstyle auth-lib:findbugs
 */

apply plugin: 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

apply plugin: 'maven'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += files("build/generated/source/r/release")
    options.links("http://docs.oracle.com/javase/7/docs/api/")
    destinationDir = file("../docs/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

if (signingEnabled == "true") {
    signing {
        sign configurations.archives
    }
}

project.ext["ossrhUsername"] = ''
project.ext["ossrhPassword"] = ''

def getSigningVariables() {
    // Try to fetch the values from local.properties, otherwise look in the environment variables
    // More info here: https://central.sonatype.org/publish/requirements/gpg/
    File secretPropsFile = project.rootProject.file('local.properties')
    if (secretPropsFile.exists()) {
        Properties p = new Properties()
        new FileInputStream(secretPropsFile).withCloseable { is ->
            p.load(is)
        }
        p.each { name, value ->
            project.ext[name] = value
        }
    } else {
        project.ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
        project.ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    }
}

/*
    Deployment to OSSRH repository
    1. run: ./gradlew uploadArchives -PsigningEnabled=true
    2. login to https://s01.oss.sonatype.org/ (you need to have access to auth lib OSSRH repository)
    3. if everything looks correctly, close and then release the staging repository.
    More info here: https://central.sonatype.org/publish/release/
 */
uploadArchives {
    repositories {
        mavenDeployer {
            getSigningVariables()

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.ext["ossrhUsername"], password: project.ext["ossrhPassword"])
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.ext["ossrhUsername"], password: project.ext["ossrhPassword"])
            }

            pom.project {
                name project.group + ':' + project.archivesBaseName
                packaging 'aar'
                description 'Spotify authorization library for Android'
                url 'https://github.com/spotify/android-auth'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    connection 'scm:git:https://github.com/spotify/android-auth.git'
                    developerConnection 'scm:git:git@github.com:spotify/android-auth.git'
                    url 'https://github.com/spotify/android-auth'
                }

                developers {
                    developer {
                        id 'erikg'
                        name 'Erik Ghonyan'
                        email 'erikg@spotify.com'
                    }
                }
            }
        }
    }
}
