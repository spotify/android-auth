/*
 * Copyright (c) 2015-2016 Spotify AB
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'com.android.library'

project.group = 'com.spotify.android'
project.archivesBaseName = 'auth'
project.version = '2.2.0'

android {
    compileSdk 33
    buildToolsVersion = '33.0.0'

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 33
        buildConfigField 'String', 'LIB_VERSION_NAME', "\"$project.version\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    flavorDimensions.add("auth")
    productFlavors {
        store {
            dimension "auth"
            versionNameSuffix "-store"
        }
        auth {
            getIsDefault().set(true)
            dimension "auth"
        }
    }

    libraryVariants.configureEach { libraryVariant ->
        libraryVariant.outputs.all { output ->
            // Rename auth-auth-[buildtype].aar to auth-[buildtype].aar
            if (libraryVariant.name.startsWith("auth")) {
                outputFileName = "auth-${libraryVariant.buildType.name}.aar"
            }
        }
    }

    lintOptions {
        lintConfig file("${project.rootDir}/config/lint.xml")
        quiet false
        warningsAsErrors false
        textReport true
        textOutput 'stdout'
        xmlReport false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    def manifestPlaceholdersForTests = [redirectSchemeName: "spotify-sdk", redirectHostName: "auth"]
    namespace 'com.spotify.sdk.android.auth'
    unitTestVariants.configureEach {
        it.mergedFlavor.manifestPlaceholders += manifestPlaceholdersForTests
    }
}

dependencies {
    implementation 'androidx.browser:browser:1.5.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.robolectric:robolectric:4.11.1'
}

/*
    Static analysis section
    run: ./gradlew auth-lib:checkstyle auth-lib:findbugs
 */

apply plugin: 'checkstyle'

tasks.register('checkstyle', Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    sign publishing.publications
}

project.ext["ossrhUsername"] = ''
project.ext["ossrhPassword"] = ''

def getSigningVariables() {
    // Try to fetch the values from local.properties, otherwise look in the environment variables
    // More info here: https://central.sonatype.org/publish/requirements/gpg/
    File secretPropsFile = project.rootProject.file('local.properties')
    if (secretPropsFile.exists()) {
        Properties p = new Properties()
        new FileInputStream(secretPropsFile).withCloseable { is ->
            p.load(is)
        }
        p.each { name, value ->
            project.ext[name] = value
        }
    } else {
        project.ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
        project.ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    }
}

/*
    Deployment to OSSRH repository
    1. run: ./gradlew :auth-lib:assembleRelease and then ./gradlew :auth-lib:publishAuthReleasePublicationToMavenRepository
       or ./gradlew :auth-lib:publishStoreReleasePublicationToMavenRepository depending on
       which flavor of the auth library you want to release.
    2. login to https://s01.oss.sonatype.org/ (you need to have access to auth lib OSSRH repository)
    3. if everything looks correctly, close and then release the staging repository.
    More info here: https://central.sonatype.org/publish/release/
 */
afterEvaluate {
    publishing {
        repositories {
            maven {
                getSigningVariables()

                def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = isReleaseVersion ? releaseRepo : snapshotRepo
                credentials {
                    username = project.ext["ossrhUsername"]
                    password = project.ext["ossrhPassword"]
                }
            }
        }

        android.libraryVariants.configureEach { variant ->
            if (variant.buildType.name == "debug") return

            def flavored = variant.flavorName != "auth"

            def javaDocDir = "../docs/"
            if (flavored) {
                javaDocDir = "../docs-${variant.flavorName}/"
            }

            def sourceDirs = variant.sourceSets.collect {
                it.javaDirectories + it.resourcesDirectories
            }

            def javadoc = task("${variant.name}Javadoc", type: Javadoc) {
                description "Generates Javadoc for ${variant.name}."
                source = variant.javaCompile.source
                destinationDir = file(javaDocDir)
                classpath += variant.javaCompileProvider.get().classpath
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
                classpath += files("build/generated/source/r/debug")
                options.links("http://docs.oracle.com/javase/7/docs/api/")
                options.links("http://d.android.com/reference/")
                failOnError false
            }

            def javadocJar = task("${variant.name}JavadocJar", type: Jar, dependsOn: javadoc) {
                archiveClassifier.set('javadoc')
                from javadoc.destinationDir
            }

            def sourcesJar = task("${variant.name}SourcesJar", type: Jar) {
                from sourceDirs
                archiveClassifier.set('sources')
            }

            publications {
                "${variant.flavorName}Release"(MavenPublication) {
                    from components."${variant.flavorName}Release"
                    String suffix = "${flavored ? "-" + variant.flavorName : ""}"
                    artifact javadocJar
                    artifact sourcesJar
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName + suffix

                    pom {
                        name = project.group + ':' + project.archivesBaseName + suffix
                        def descriptionSuffix = ""
                        if (variant.flavorName == "store") {
                            descriptionSuffix = " with the Play Store Fallback"
                        }
                        description = 'Spotify authorization library for Android' + descriptionSuffix
                        with configurePom()
                    }
                }
            }
        }
    }
}

def configurePom() {
    return {
        packaging = 'aar'
        url = 'https://github.com/spotify/android-auth'

        licenses {
            license {
                name = 'The Apache Software License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        scm {
            connection = 'scm:git:https://github.com/spotify/android-auth.git'
            developerConnection = 'scm:git:git@github.com:spotify/android-auth.git'
            url = 'https://github.com/spotify/android-auth'
        }

        developers {
            developer {
                id = 'erikg'
                name = 'Erik Ghonyan'
                email = 'erikg@spotify.com'
            }
        }
    }
}
